#!/usr/bin/env perl

use strict;
use warnings;
use Data::Dumper qw(Dumper);
use English qw(LAST_PAREN_MATCH PROGRAM_NAME);

for my $username_or_uid (@ARGV ? @ARGV : `getent passwd` =~ /^[^:]+/msxg) {
    # Make sure the argument won't inadverently cause a shell to run commands.
    unless ($username_or_uid =~ /\A\w[\w-]*\z/msx) {
        warn qq{$PROGRAM_NAME: bad username or uid "$username_or_uid"\n};
        next;
    }

    if (`getent passwd $username_or_uid` =~ /^(?<user>[^:\n]+)
                                             :(?<pass>[^:\n]*)
                                             :(?<uid>\d+)
                                             :(?<gid>\d+)
                                             :(?<gecos>[^:\n]*)
                                             :(?<home>[^:\n]+)
                                             :(?<shell>[^:\n]+)$/msx) {
        print Dumper(\%LAST_PAREN_MATCH);
    } else {
        warn qq{$PROGRAM_NAME: username or uid "$username_or_uid" not found\n};
    }
}
